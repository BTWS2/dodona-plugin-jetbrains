plugins {
    id 'idea'
    id 'java'
    id 'org.jetbrains.intellij' version '0.3.10'
}

group 'be.ugent.piedcler.dodona'
version '1.1.1-SNAPSHOT'

sourceCompatibility = 1.8

def hasPyCharm = project.hasProperty('pycharmPath')

intellij {
    version ideaVersion
    pluginName 'ugent-dodona'
    updateSinceUntilBuild false
    if (hasPyCharm){
        alternativeIdePath pycharmPath
    }else{
        plugins += [pythonPlugin]
    }
}

repositories {
    mavenCentral()

    maven {
        url  "https://dl.bintray.com/thepieterdc/dodona-api-java"
    }

    if (hasPyCharm){
        flatDir {
            dirs "$pycharmPath/lib"
        }
    }
}

dependencies {
    compile group: 'be.ugent.piedcler.dodona', name: 'dodona-api-interface', version: '1.0.0'
    runtime group: 'be.ugent.piedcler.dodona', name: 'dodona-api-impl', version: '1.0.0'

    if (hasPyCharm){
        compileOnly name: 'pycharm'
    }
}

patchPluginXml {
    version project.version
}
